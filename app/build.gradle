apply plugin: 'com.android.application'

android {

    packagingOptions {
        exclude 'META-INF/android.arch.persistence_db-framework.version'
        exclude 'META-INF/android.arch.persistence_db.version'
        exclude 'META-INF/androidx.sqlite_sqlite-framework.version'
        exclude 'META-INF/androidx.sqlite_sqlite.version'
    }

    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.example.readersdk"
        minSdkVersion 23
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 1020
        versionName "1.3"

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries = true
        jumboMode = true
        keepRuntimeAnnotatedClasses false
    }

}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            // Force all of the primary support libraries to use the same version.
            if (details.requested.group == 'com.android.support'
                    && details.requested.name != 'multidex'
                    && details.requested.name != 'multidex-instrumentation') {
                details.useVersion '28.0.0'
            }
        }
    }
}


repositories {
    google()
    maven {
        url "https://sdk.squareup.com/android"
        credentials {
            // SQUARE_READER_SDK_APPLICATION_ID is defined in ./gradle.properties
            username SQUARE_READER_SDK_APPLICATION_ID
            // SQUARE_READER_SDK_REPOSITORY_PASSWORD is defined in ./gradle.properties
            password SQUARE_READER_SDK_REPOSITORY_PASSWORD
        }
    }
    jcenter()
}

dependencies {

    def readerSdkVersion = "1.3.3"
    // SQUARE_READER_SDK_APPLICATION_ID is defined in ./gradle.properties
    implementation "com.squareup.sdk.reader:reader-sdk-$SQUARE_READER_SDK_APPLICATION_ID:$readerSdkVersion"
    runtimeOnly "com.squareup.sdk.reader:reader-sdk-internals:$readerSdkVersion"

    implementation 'com.github.bhargavms:DotLoader:1.1.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.13-beta-3'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'androidx.room:room-runtime:2.2.1'
    annotationProcessor 'androidx.room:room-compiler:2.2.1'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.android.volley:volley:1.1.0'

    implementation 'com.getbase:floatingactionbutton:1.10.1'

}
